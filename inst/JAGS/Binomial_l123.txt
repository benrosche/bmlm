model {
  
  # Likelihood
  for(j in 1:n.l2) {
    # Logistic regression
    Y[j] ~ dbern(p[j])
    # Link function
    logit(p[j]) <- inprod(l1[l1i1[j]:l1i2[j]], w[l1i1[j]:l1i2[j]]) + l2[j] + l3[l3id[j]]
    # Level 2: systematic component
    l2[j] <- inprod(X.l2[j,], b.l2)
  }
  
  # Level 1: systematic + random component
  for(i in 1:n.l1) {
    l1[i] <- inprod(X.l1[i,], b.l1) + re.l1[l1id[i]] 
    # Weight function
    uw[i] <- 1/X.w[i,1]
    w[i] <- uw[i] / sum(uw[l1i1.l1[i]:l1i2.l1[i]])
  }
  
  # Level 3: systematic + random component
  for (k in 1:n.l3) {
    l3[k] <- inprod(X.l3[k,], b.l3) + re.l3[k] 
  }
  
  # Random effects
  for (i in 1:n.ul1) {
    re.l1[i] ~ dnorm(0, tau.l1)
  }
  
  for (k in 1:n.l3) {
    re.l3[k] ~ dnorm(0, tau.l3)
  }
  
  # Prior: regression coefficients
  for(x in 1:n.Xl1) {
    b.l1[x] ~ dnorm(0,0.0001)
  }
  
  for(x in 1:n.Xl2) {
    b.l2[x] ~ dnorm(0,0.0001)
  }
  
  for(x in 1:n.Xl3) {
    b.l3[x] ~ dnorm(0,0.0001)
  }
  
  # b.w (placeholder)
  
  # Prior: variance terms
  tau.l1 ~ dscaled.gamma(25, 1)
  sigma.l1 <- 1/sqrt(tau.l1)
  
  sigma.l2 <- 3.14159^2/3 # Variance of logistic distribution 
  
  tau.l3 ~ dscaled.gamma(25, 1)
  sigma.l3 <- 1/sqrt(tau.l3)
  
}
