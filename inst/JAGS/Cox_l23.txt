model {
  
  # Likelihood
  for(r in 1:n.tu) {
    for(j in 1:n.l2) {
      dN[j,r] ~ dpois(Idt[j,r]); # Poisson trick
      Idt[j,r] <- Y[j,r] * dL0[r] * exp(inprod(inprod(X.l2[j,], b.l2) + l3[l3id[j]])
    }
    # Prior on the increments of the log-hazard
    dL0[r] ~ dgamma(mu[r], c)
    mu[r] <- dL0.star[r] * c
    dL0.star[r] <- d * (t.unique[r+1]-t.unique[r]) 
  }
  
  # Level 3: systematic + random component
  for (k in 1:n.l3) {
    l3[k] <- inprod(X.l3[k,], b.l3) + re.l3[k] 
  }
  
  # Random effects
  for (k in 1:n.l3) {
    re.l3[k] ~ dnorm(0, tau.l3)
  }
  
  # Prior: regression coefficients
  for(x in 1:n.Xl2) {
    b.l2[x] ~ dnorm(0,0.0001)
    ppp.b.l2[x] <- step(b.l2[x])
  }
  
  for(x in 1:n.Xl3) {
    b.l3[x] ~ dnorm(0,0.0001)
    ppp.b.l3[x] <- step(b.l3[x])
  }
  
  # Prior: variance terms
  tau.l3 ~ dscaled.gamma(25, 1)
  sigma.l3 <- 1/sqrt(tau.l3)

}
