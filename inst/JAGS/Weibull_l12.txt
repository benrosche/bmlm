model {
  
  # Likelihood
  for(j in 1:n.l2) {
    # Weibull regression
    censored[j] ~ dinterval(t[j], ct.lb[j])  
    t[j] ~ dweib(shape, lambda[j])
    # Link function
    log(lambda[j]) <- -(inprod(l1[l1i1[j]:l1i2[j]], w[l1i1[j]:l1i2[j]]) + l2[j])*shape
    # Level 2: systematic component
    l2[j] <- inprod(X.l2[j,], b.l2)
  }
  
  # Level 1: systematic + random component
  for(i in 1:n.l1) {
    l1[i] <- inprod(X.l1[i,], b.l1) + re.l1[l1id[i]] 
    # Weight function
    uw[i] <- 1/X.w[i,1]
    w[i] <- uw[i] / sum(uw[l1i1.l1[i]:l1i2.l1[i]])
  }
  
  # Random effects
  for (i in 1:n.ul1) {
    re.l1[i] ~ dnorm(0, tau.l1)
  }
  
  # Prior: regression coefficients
  for(x in 1:n.Xl1) {
    b.l1[x] ~ dnorm(0,0.0001)
  }
  
  for(x in 1:n.Xl2) {
    b.l2[x] ~ dnorm(0,0.0001)
  }
  
  # b.w (placeholder)
  
  # Prior: variance terms
  tau.l1 ~ dscaled.gamma(25, 1)
  sigma.l1 <- 1/sqrt(tau.l1)
  
  shape ~ dgamma(0.01, 0.01)
  sigma.l2 <- 3.14159^2/(6*shape^2)
  
}
