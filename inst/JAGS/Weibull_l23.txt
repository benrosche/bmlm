model {
  
  # Likelihood
  for(j in 1:n.l2) {
    
    # Weibull regression
    censored[j] ~ dinterval(t[j], ct.lb[j])  
    t[j] ~ dweib(shape, lambda[j])
    
    # Link function
    log(lambda[j]) <- -(l2[j] + l3[l3id[j]])*shape
    
    # Level 2: systematic component
    l2[j] <- inprod(X.l2[j,], b.l2)
  }
  
  # Level 3: systematic + random component
  for (k in 1:n.l3) {
    l3[k] <- inprod(X.l3[k,], b.l3) + re.l3[k] 
  }
  
  # Random effects
  for (k in 1:n.l3) {
    re.l3[k] ~ dnorm(0, tau.l3)
  }
  
  # Prior: regression coefficients
  for(x in 1:n.Xl2) {
    b.l2[x] ~ dnorm(0,0.0001)
  }
  
  for(x in 1:n.Xl3) {
    b.l3[x] ~ dnorm(0,0.0001)
  }
  
  # Prior: variance terms
  shape ~ dgamma(0.01, 0.01)
  sigma.l2 <- 3.14159^2/(6*shape^2)
  
  tau.l3 ~ dscaled.gamma(25, 1)
  sigma.l3 <- 1/sqrt(tau.l3)
  
}